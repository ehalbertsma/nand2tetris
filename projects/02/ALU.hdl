// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
   
    Mux16 (a=x[0..15], b=false, sel=zx, out=zerox);
    Not16 (in=zerox, out=notx);
    Mux16 (a=zerox, b=notx, sel=nx, out=zeronotx); 

    Mux16 (a=y[0..15], b=false, sel=zy, out=zeroy);
    Not16 (in=zeroy, out=noty);
    Mux16 (a=zeroy, b=noty, sel=ny, out=zeronoty); 

    Add16 (a=zeronotx, b=zeronoty, out=xplusy);
    And16 (a=zeronotx, b=zeronoty, out=xandy);

    Mux16 (a=xandy, b=xplusy, sel=f, out=xyout);
    Not16 (in=xyout, out=notxyout);

    Mux16 (a=xyout, b=notxyout, sel=no, 
        out[15]=ng, out[0..7]=tempout1, out[8..15]=tempout2, out=out[0..15]);

    Or8Way (in=tempout1, out=zr1);
    Or8Way (in=tempout2, out=zr2);
    Or (a=zr1, b=zr2, out=zr0);
    Not (in=zr0, out=zr);
}
